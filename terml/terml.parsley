horizontal_space = (' '|'\t'|'\f'|('#' (~eol anything)*))
spaces = horizontal_space*

number = spaces barenumber
barenumber = '-'?:sign (('0' ((('x'|'X') hexdigit*:hs -> makeHex(sign, hs))
                    |floatPart(sign '0')
                    |octaldigit*:ds -> makeOctal(sign, ds)))
               |decdigits:ds floatPart(sign ds)
               |decdigits:ds -> signedInt(sign, ds))


exponent = <('e' | 'E') ('+' | '-')? decdigits>


floatPart :sign :ds = <('.' decdigits exponent?) | exponent>:tail -> makeFloat(sign, ds, tail)

decdigits = digit:d ((:x ?(isDigit(x)) -> x) | '_' -> "")*:ds -> concat(d, join(ds))
octaldigit = :x ?(isOctDigit(x)) -> x
hexdigit = :x ?(isHexDigit(x)) -> x

string = token('"') (escapedChar | ~('"') anything)*:c '"' -> join(c)
character = token("'") (escapedChar | ~('\''|'\n'|'\r'|'\\') anything):c '\'' -> Character(c)
escapedUnicode = ('u' <hexdigit hexdigit hexdigit hexdigit>:hs -> unichr(int(hs, 16))
                   |'U' <hexdigit hexdigit hexdigit hexdigit
                         hexdigit hexdigit hexdigit hexdigit>:hs -> unichr(int(hs, 16)))

escapedOctal = ( <:a ?(contains("0123", a)) octdigit? octdigit?>
                 | <:a ?(contains("4567", a)) octdigit?>):os -> int(os, 8)

escapedChar = '\\' ('n' -> '\n'
                     |'r' -> '\r'
                     |'t' -> '\t'
                     |'b' -> '\b'
                     |'f' -> '\f'
                     |'"' -> '"'
                     |'\'' -> '\''
                     |'?' -> '?'
                     |'\\' -> '\\'
                     | escapedUnicode
                     | escapedOctal
                     | eol -> "")

eol = horizontal_space* ('\r' '\n'|'\r' | '\n')

uriBody = <(letterOrDigit |';'|'/'|'?'|':'|'@'|'&'|'='|'+'|'$'|','|'-'|'.'|'!'|'~'|'*'|'\''|'('|')'|'%'|'\\'|'|'|'#')+>

spaces = ('\r' '\n'|'\r' | '\n' | horizontal_space)*
literal = string:x -> leafInternal(Tag(".String."), x)
        | character:x -> leafInternal(Tag(".char."), x)
        | number:x -> leafInternal(Tag(numberType(x)), x)

tag =  (
          segment:seg1 (':' ':' sos)*:segs -> makeTag(cons(seg1, segs))
        | (':' ':' sos)+:segs -> prefixedTag(segs))

sos = segment | (string:s -> tagString(s))

segment = ident | special | uri

ident = segStart:i1 segPart*:ibits -> join(cons(i1, ibits))

segStart = letter | '_' | '$'

segPart = letterOrDigit | '_' | '.' | '-' | '$'

special = '.':a ident:b -> concat(a, b)

uri = '<' uriBody*:uriChars '>' -> concat(b, uriChars, e)

functor = spaces (literal | tag:t -> leafInternal(t, None))
baseTerm = functor:f ('(' argList:a spaces ')' -> makeTerm(f, a)
                     | -> makeTerm(f, None))

arg = term

argList = ((arg:t (token(',') arg)*:ts token(',')?) -> cons(t, ts)
            | -> [])

tupleTerm = token('[') argList:a token(']') -> Tuple(a)

bagTerm = token('{') argList:a token('}') -> Bag(a)

labelledBagTerm = functor:f bagTerm:b -> LabelledBag(f, b)

extraTerm = tupleTerm | labelledBagTerm  | bagTerm | baseTerm

attrTerm = extraTerm:k token(':') extraTerm:v -> Attr(k, v)

term =  spaces (attrTerm | extraTerm)
